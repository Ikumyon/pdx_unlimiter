plugins {
    id 'application'
    id "org.moditect.gradleplugin" version "1.0.0-rc3"
    id 'org.openjfx.javafxplugin' version '0.0.9'
}

setVersion '1.0-rc3'

if (!file("${projectDir}/pdxu.properties").exists()) {
    file("${projectDir}/pdxu.properties").write(file("${projectDir}/pdxu.properties.default").getText())
}

java {
    modularity.inferModulePath = true
    sourceCompatibility = JavaVersion.VERSION_15
    targetCompatibility = JavaVersion.VERSION_15
}

application {
    mainModule = 'com.crschnick.pdx_unlimiter.app'
    mainClass = 'com.crschnick.pdx_unlimiter.app.PdxuApp'
}

javafx {
    version = '15'
    modules = [ 'javafx.controls', 'javafx.base', 'javafx.graphics', 'javafx.swing' ]
}

repositories {
    mavenCentral()
    maven {
        url = 'https://maven.nikr.net/'
    }
    maven { url "https://jitpack.io" }
}

dependencies {
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: "2.11.3"
    implementation files("$buildDir/generated-modules/com.realityinteractive.imageio.tga-master-SNAPSHOT.jar")
    implementation files("$buildDir/generated-modules/dds-1.0.0.jar")
    implementation files("$buildDir/generated-modules/commons-lang3-3.11.jar")
    implementation files("$buildDir/generated-modules/commons-io-2.8.0.jar")
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.0-alpha1'
    implementation group: 'org.slf4j', name: 'slf4j-simple', version: '2.0.0-alpha1'
    implementation files("$buildDir/generated-modules/sentry-1.7.30.jar")
    implementation files("$buildDir/generated-modules/jnativehook-2.1.0.jar")
    implementation files("$buildDir/generated-modules/json-path-master-SNAPSHOT.jar")
    //implementation 'com.github.json-path:JsonPath:master-SNAPSHOT'
    implementation files("$buildDir/generated-modules/json-smart-2.3.jar")
    implementation files("$buildDir/generated-modules/accessors-smart-1.2.jar")
    implementation project(':eu4')
}

addDependenciesModuleInfo {
    overwriteExistingFiles = true
    jdepsExtraArgs = ['-q']
    outputDirectory = file("$buildDir/generated-modules")
    modules {
        module {
            artifact group: 'net.minidev', name: 'json-smart', version: '2.3'
            moduleInfoSource = '''
                module net.minidev.json {
                    exports net.minidev.json;
                    exports net.minidev.json.parser;
                    requires net.minidev.asm;
                }
            '''
        }
        module {
            artifact group: 'net.minidev', name: 'accessors-smart', version: '1.2'
            moduleInfoSource = '''
                module net.minidev.asm {
                    exports net.minidev.asm;
                }
            '''
        }
        module {
            artifact 'com.github.json-path.JsonPath:json-path:master-SNAPSHOT'
            moduleInfoSource = '''
                module com.jayway.jsonpath {
                    exports com.jayway.jsonpath;
                    exports com.jayway.jsonpath.spi.json;
                    exports com.jayway.jsonpath.spi.mapper;
                    requires com.fasterxml.jackson.databind;
                    requires org.slf4j;
                    requires net.minidev.json;
                }
            '''
        }
        module {
            artifact group: 'com.1stleg', name: 'jnativehook', version: '2.1.0'
            moduleInfoSource = '''
                module org.jnativehook {
                    exports org.jnativehook;
                    exports org.jnativehook.keyboard;
                   requires java.logging;
                   requires java.desktop;
                }
            '''
        }
        module {
            artifact 'io.sentry:sentry:1.7.30'
            moduleInfoSource = '''
                module io.sentry {
                    exports io.sentry;
                    requires org.slf4j;
                    requires java.naming;
                    requires com.fasterxml.jackson.core;
                }
            '''
        }
        module {
            artifact 'com.github.tmyroadctfig:com.realityinteractive.imageio.tga:master-SNAPSHOT'
            moduleInfoSource = '''
                module com.realityinteractive.imageio.tga {
                    requires java.se;
                    exports com.realityinteractive.imageio.tga;
                }
            '''
        }
        module {
            artifact 'net.nikr:dds:1.0.0'
            moduleInfoSource = '''
                module net.nikr.dds {
                    requires java.se;
                    exports net.nikr.dds;
                    provides javax.imageio.spi.ImageReaderSpi with net.nikr.dds.DDSImageReaderSpi;
                }
            '''
        }
        module {
            artifact 'org.apache.commons:commons-lang3:3.11'
            moduleInfoSource = '''
                module org.apache.commons.lang3 {
                    requires java.se;
                    exports org.apache.commons.lang3;
                    exports org.apache.commons.lang3.function;
                    exports org.apache.commons.lang3.arch;
                    exports org.apache.commons.lang3.reflect;
                }
            '''
        }
        module {
            artifact 'commons-io:commons-io:2.8.0'
            moduleInfoSource = '''
                module org.apache.commons.io {
                    requires java.se;
                    exports org.apache.commons.io;
                    exports org.apache.commons.io.file;
                }
            '''
        }
    }
}

task copyModules(type: Copy) {
    into "${buildDir}/modules"
    from configurations.runtimeClasspath
}

task copyOutput(type: Copy) {
    into "${buildDir}/modules"
    from "${buildDir}/libs"
}

createRuntimeImage.dependsOn([copyModules, copyOutput])
createRuntimeImage {
    outputDirectory = file("$buildDir/image")
    modulePath = [file("$buildDir/modules")]
    modules = ['com.crschnick.pdx_unlimiter.app', 'org.slf4j', 'org.slf4j.simple', 'jdk.crypto.ec']
    excludedResources = []
    launcher {
        name = 'pdxu'
        module = 'com.crschnick.pdx_unlimiter.app'
    }
    compression = 2
    stripDebug = true
    noHeaderFiles = true
    noManPages = true
}

task writeVersion(type: DefaultTask) {
    doLast {
        mkdir "${buildDir}/image/"
        file("${buildDir}/image/version").write(String.valueOf(version))
    }
}

task copySentryProperties(type: Copy) {
    into file("${buildDir}/image/bin/")
    from file("${projectDir}/sentry_prod.properties")
    rename { f -> return "sentry.properties"}
}

task copyPdxuProperties(type: Copy) {
    into file("${buildDir}/image/")
    from file("${projectDir}/pdxu_prod.properties")
    rename { f -> return "pdxu.properties"}
}

createRuntimeImage.finalizedBy([writeVersion, copySentryProperties, copyPdxuProperties])

task createDist(type: Zip, dependsOn: createRuntimeImage) {
    destinationDirectory = buildDir
    archivesBaseName = "pdx_unlimiter"
    archiveAppendix = "windows"
    from "${buildDir}/image"
}

